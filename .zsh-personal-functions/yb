#!/usr/bin/env zsh
yb() {
    local logdir
    if [[ -n $UNAME_MACOS ]]; then
        logdir=$HOME/Library/Logs/yb
    else
        logdir=${XDG_STATE_HOME:-$HOME/.local/state}/yb
    fi
    if [ ! -d $logdir ]; then
        mkdir -p $logdir
    fi
    local logfile
    logfile=$logdir/yadm_log

    if command -v newsyslog &>/dev/null; then
        newsyslog -r -v -f "${XDG_CONFIG_HOME:-$HOME/.config}"/yadm/newsyslog.conf $logfile
    elif command -v logrotate &>/dev/null; then
        logrotate -v -s "${XDG_STATE_HOME:-$HOME/.local/state}/logrotate.status" "${XDG_CONFIG_HOME:-$HOME/.config}/yadm/logrotate.conf"
    fi

    $HOME/bin/yadm-fsmonitor

    # check if I already have sudo rights
    if [[ -n $UNAME_MACOS ]]; then
        local tmpfile=$(mktemp /tmp/yb-script.XXXXXX)
        (
            (
                set -e
                if [[ -z "$KLA" ]]; then
                    if ! sudo -n true 2>/dev/null; then
                        # if not, request sudo login right away
                        sudo -v
                    fi
                fi
                echo "\n$(date +%F\ %T)"
                script -q /dev/null yadm pull
                if [[ "$(yadm --version | tail -1)" == "yadm version 3"* ]]; then
                    script -q /dev/null yadm bootstrap
                else
                    script -q /dev/null $HOME/.config/yadm/bootstrap
                fi
                if [[ -z "$KLA" ]]; then
                    sudo script -q /dev/null ~/.config/yadm/bootstrap-sudo
                fi
                script -q /dev/null yadm push
            )

            echo $? >$tmpfile

        ) 2>&1 | tee -a $logfile
        return $(cat $tmpfile)
    else
        if ! sudo -n true 2>/dev/null; then
            # if not, request sudo login right away
            sudo -v
        fi
        echo "\n$(date +%F\ %T)" >>$logfile &&
            script -qefa $logfile -c 'yadm pull' &&
            (
                if [[ "$(yadm --version | tail -1)" == "yadm version 3"* ]]; then
                    script -qefa $logfile -c "yadm bootstrap"
                else
                    script -qefa $logfile -c "$HOME/.config/yadm/bootstrap"
                fi
            ) &&
            script -qefa $logfile -c "$HOME/.config/yadm/bootstrap-sudo" &&
            script -qefa $logfile -c "yadm push"
    fi
}
yb "$@"
